.SUBCKT huf75842  2 1 3      
*Nom Temp=25 deg C   
*13 Oct 99
Ca 12 8 4.10e-9
Cb 15 14 4.10e-9
Cin 6 8 2.50e-9

Dbody 7 5 DbodyMOD
Dbreak 5 11 DbreakMOD
Dplcap 10 5 DplcapMOD

Ebreak 11 7 17 18 157.5
Eds 14 8 5 8 1
Egs 13 8 6 8 1
Esg 6 10 6 8 1
Evthres 6 21 19 8 1
Evtemp 20 6 18 22 1

It 8 17 1

Lgate 1 9 4.86e-9
Ldrain 2 5 1.0e-9
Lsource 3 7 2.01e-9

RLgate 1 9 48.6
RLdrain 2 5 10
RLsource 3 7 20.1

Mmed 16 6 8 8 MmedMOD
Mstro 16 6 8 8 MstroMOD 
Mweak 16 21 8 8 MweakMOD
    
Rbreak 17 18 RbreakMOD 1
Rdrain 50 16 RdrainMOD 2.72e-2
Rgate 9 20 0.73
RSLC1 5 51 RSLCMOD 1e-6
RSLC2 5 50 1e3
Rsource 8 7 RsourceMOD 3.58e-3
Rvthres 22 8 RvthresMOD 1
Rvtemp 18 19 RvtempMOD 1

S1a 6 12 13 8 S1AMOD
S1b 13 12 13 8 S1BMOD
S2a 6 15 14 13 S2AMOD
S2b 13 15 14 13 S2BMOD

Vbat 22 19 DC 1

ESLC 51 50  VALUE={(V(5,51)/ABS(V(5,51)))*(PWR(V(5,51)/(1e-6*88),3.5))}


.MODEL DbodyMOD D (IS=2.25e-12 RS=2.45e-3 IKF=14 XTI=5 TRS1=2.7e-3 TRS2=0 CJO=2.60e-9 TT=1.22e-7 M=0.55)
.MODEL DbreakMOD D (RS=6.50e-1 TRS1=1e-3 TRS2=1e-6)
.MODEL DplcapMOD D (CJO=3.30e-9 IS=1e-30 M=0.82)
.MODEL MmedMOD NMOS (VTO=3.20 KP=6 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=0.73)
.MODEL MstroMOD NMOS (VTO=3.63 KP=86 IS=1e-30 N=10 TOX=1 L=1u W=1u)
.MODEL MweakMOD NMOS (VTO=2.78 KP=0.10 IS=1e-30 N=10 TOX=1 L=1u W=1u RG=7.3) 


.MODEL RbreakMOD RES (TC1=1.02e-3 TC2=0)
.MODEL RdrainMOD RES (TC1=9.40e-3 TC2=2.70e-5)
.MODEL RSLCMOD RES (TC1=4.10E-3 TC2=4.00E-6)
.MODEL RsourceMOD RES (TC1=1e-3 TC2=1e-6)
.MODEL RvthresMOD RES (TC1=-2.57e-3 TC2=-7.05e-6)
.MODEL RvtempMOD RES (TC1=-2.75e-3 TC2=9.00e-7)

.MODEL S1AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-5.8 VOFF=-2.4)
.MODEL S1BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-2.4 VOFF=-5.8)
.MODEL S2AMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=-1.8 VOFF=0.5)
.MODEL S2BMOD VSWITCH (RON=1e-5 ROFF=0.1 VON=0.5 VOFF=-1.8)

.ENDS

*Thermal Model Subcircuit
*HUF75842
*10/13/99
.SUBCKT huf75842t TH TL

CTHERM1 TH c2 5.2e-3	   
CTHERM2 c2 c3 2.4e-2
CTHERM3 c3 c4 2.0E-2
CTHERM4 c4 c5 1.8E-2
CTHERM5 c5 c6 2.4E-2
CTHERM6 c6 TL 1.8e-1

RTHERM1 TH c2 1.0E-2
RTHERM2 c2 c3 2.0e-2
RTHERM3 c3 c4 6.4e-2
RTHERM4 c4 c5 1.0e-1
RTHERM5 c5 c6 1.56e-1
RTHERM6 c6 TL 1.65e-1
.ENDS

